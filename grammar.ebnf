program
    ::= sequence EOF

sequence
    ::= (expression ";")*

expression
    ::= assignment

assignment
    ::= IDENTIFIER "=" assignment
     | logic_or

logic_or
    ::= logic_and ( "or" logic_and )*

logic_and
    ::= equality ( "and" equality )*

equality
    ::= comparison ( ( "==" | "!=" ) comparison )*

comparison
    ::= term ( ( ">" | ">=" | "<" | "<=" ) term )*

term
    ::= factor ( ( "+" | "-" ) factor )*

factor
    ::= unary ( ( "*" | "/" ) unary )*

unary
    ::= ( "!" | "-" ) unary
     | primary

primary
    ::= literal
     | IDENTIFIER
     | "(" expression ")"
     | if_expr
     | block
     | call
     | var_expr

literal
    ::= NUMBER
     | STRING
     | "true"
     | "false"
     | "none"

var_expr
    ::= "var" IDENTIFIER "=" expression

if_expr
    ::= "if" "(" expression ")" expression "else" expression

block
    ::= "{" sequence "}"

call
    ::= primary "(" arguments? ")"

arguments
    ::= expression ( "," expression )*

